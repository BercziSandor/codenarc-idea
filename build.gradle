plugins {
    id 'org.jetbrains.intellij' version '0.4.11'
    id 'java'
    id 'idea'
    id 'groovy'
    id 'enterprise.groovy.plugin' version '1.0.3'
}

if (!project.hasProperty('jbPluginsToken')) ext.jbPluginsToken = '**UNDEFINED**'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

group 'org.codenarc.idea'
version "${pluginVersion}"

repositories {
    mavenCentral()
}

idea {
    project {
        jdkName = '11'
    }
}

configurations {
    implementation  {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}

dependencies {
    compileOnly( 'org.codehaus.groovy:groovy-all') {
        version {
            strictly "$groovyVersion"
        }
        because 'there are version conflicts when building'
    }
    implementation('org.codenarc:CodeNarc:1.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy-ant'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    runtimeOnly('org.gmetrics:GMetrics:1.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy-ant'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }

    compile 'org.apache.commons:commons-lang3:3.9'

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.4'
}

intellij {
    pluginName 'CodeNarc'
    downloadSources true
    plugins 'Groovy', 'gradle', 'java'
}

publishPlugin {
    token "${jbPluginsToken}"
}

patchPluginXml {
    version "${pluginVersion}"
    sinceBuild '192.4147'
    untilBuild null
    changeNotes file('changelog.html').text
    pluginDescription file('description.html').text
}
